#!/usr/bin/env perl

# Copyright (C) 2012  Joshua Hoblitt <jhoblitt@cpan.org>

use strict;
use warnings;

use User::pwent;
use User::grent;
use Template;
use Digest::MD5 qw( md5_base64 );
use Net::SSH::AuthorizedKey;
use Getopt::Long qw( GetOptions :config auto_help auto_version );
use Pod::Usage qw( pod2usage );

my $class_name = 'sdm::users';

GetOptions(
    'class_name|s=s'    => \$class_name,
) || pod2usage( 2 );


#my $u = shift;

my $template_path = "./templates";

my $tt = Template->new({
    INCLUDE_PATH => $template_path,
    INTERPOLATE  => 0,
}) || die "$Template::ERROR\n";

my $vars = {
    class_name => $class_name,
};

$tt->process('puppet_class_header.tt', $vars) || die $tt->error(), "\n";

my %user_list;
open(my $fh, shift) or die "can't open file: $!";
foreach my $u (<$fh>) {
    chomp $u;
    next unless $u =~ /\S/;
    $user_list{$u}++;

    process_realize_user($tt, $u);
}
close($fh) or die "can't close file: $!";

print "\n";

# hash of lists.  hash key is gid, value is a list of usernames
my %gid_list;
foreach my $u (keys %user_list) {
    process_virtual_user($tt, $u, \%gid_list);
}

# also load up the entire wheel group
my $gr = getgrnam('wheel');
if (defined $gr) {
    foreach my $user (@{$gr->members}) {
	if (defined $user_list{$user}) {
            push @{$gid_list{$gr->gid}}, $user;
        }
    }
}

process_virtual_groups($tt, \%gid_list);

$tt->process('puppet_class_footer.tt', $vars) || die $tt->error(), "\n";

sub process_realize_user {
    my ($tt, $u) = @_;

    my $vars = {
        name => $u,
    };

    $tt->process('realize_user.tt', $vars) || die $tt->error(), "\n";
}

sub process_virtual_user {
    my ($tt, $u, $gid_list) = @_;

    my $pw = getpwnam($u) || die "No user: $u";

    # store gid so we can create the virtual group types as well
    push @{$gid_list->{$pw->gid}}, $u;

    use Data::Dumper;
    #print Dumper($pw);

    my $vars = {
        name    => $pw->name,
        passwd  => $pw->passwd,
        uid     => $pw->uid,
        gid     => $pw->gid,
        gecos   => $pw->gecos,
        dir     => $pw->dir,
        shell   => $pw->shell,
    };
    $tt->process('virtual_user.tt', $vars) || die $tt->error(), "\n";

    my $keys = $pw->dir . "/.ssh/authorized_keys";

    if (-e $keys) {
    open(my $fh, $pw->dir . "/.ssh/authorized_keys") or die "can't open file: $!";
    foreach my $line (<$fh>) {
	next unless $line =~ /\S/;
        my $key = Net::SSH::AuthorizedKey->parse( $line );
        use Data::Dumper;
#         print Dumper($key);
        my $comment = $key->comment;
        chomp $comment;

        # XXX ugly hack to work around puppets use of the ssh key 'name' a globally unique id
        my $key_digest = md5_base64($key->key);
        # use only the first 4 digits as the digest
	$key_digest = substr($key_digest, 0, 4);
        $comment = $comment . " # $u $key_digest";
        $comment  =~ s/\s/_/g;
        
        my $vars = {
            key => $key->key,
            type => $key->encryption,
            username => $u,
            comment => $comment,
        };
        $tt->process('ssh_authorized_key.tt', $vars) || die $tt->error(), "\n";
    }
    close ($fh);
    }
}

sub process_virtual_groups {
    my ($tt, $gid_list) = @_;

#    print Dumper($gid_list);

    foreach my $gid (keys %{$gid_list}) {
	my $gr = getgrgid($gid);
	#print Dumper($gr);
	next unless defined $gr;

        my $vars = {
            group_name => $gr->name,
            gid        => $gid,
            members    => $gid_list->{$gid},
        };

        $tt->process('virtual_group.tt', $vars) || die $tt->error(), "\n";
    }
}

=pod

=head1 NAME

ppwent - generate puppet user/group resources from pwent()/grent() 

=head1 SYNOPSIS

    ppwent --users ./users.txt --groups ./groups.txt --class_name my::users

=head1 DESCRIPTION

=head1 CREDITS

Just me, myself, and I.

=head1 SUPPORT

Please contact the author directly via e-mail.

=head1 AUTHOR

Joshua Hoblitt <jhoblitt@cpan.org>

=head1 COPYRIGHT

Copyright (C) 2012  Joshua Hoblitt

This program is free software; you can redistribute it and/or
modify it under the terms of the GNU General Public License
as published by the Free Software Foundation; either version 2
of the License, or (at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.

The full text of the license can be found in the LICENSE file included with
this module or in the L<perlgpl> Pod included with Perl 5.8.1 or later.

=head1 SEE ALSO

L<File::Integrity>, L<File::Integrity::Stat>

=cut
