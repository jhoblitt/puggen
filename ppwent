#!/usr/bin/env perl

# Copyright (C) 2012  Joshua Hoblitt <jhoblitt@cpan.org>

use strict;
use warnings;

use User::pwent;
use User::grent;
use Template;
use Digest::MD5 qw( md5_base64 );
use Net::SSH::AuthorizedKey;
use Getopt::Long qw( GetOptions :config auto_help auto_version );
use Pod::Usage qw( pod2usage );

my $class_name = 'my::users';
my $users;
my $groups;
my $debug;

GetOptions(
    'class_name|c=s'    => \$class_name,
    'users|u=s'         => \$users,
    'groups|g=s'        => \$groups,
    'debug|d'           => \$debug,
) || pod2usage( 2 );

pod2usage(-msg => "required option(s): --users <file name> --groups <file name>", -exitval => 3)
    unless defined $users and defined $groups;
pod2usage(-msg => "unknown argument(s): @ARGV", -exitval => 4) if scalar @ARGV;
pod2usage(-msg => "--users $users: file does not exist", -exitval => 5)
    unless -e $users;
pod2usage(-msg => "--users $users: file is not a plain file", -exitval => 6)
    unless -f $users;

pod2usage(-msg => "--groups $groups: file does not exist", -exitval => 7)
    unless -e $groups;
pod2usage(-msg => "--groups $groups: file is not a plain file", -exitval => 8)
    unless -f $groups;

my $template_path = "./templates";

my $tt = Template->new({
    INCLUDE_PATH => $template_path,
    INTERPOLATE  => 0,
}) || die "$Template::ERROR\n";

my $vars = {
    class_name => $class_name,
};

my @user_list = read_config_file($users);
my @group_list = read_config_file($groups);

my %user_info;
# user_info:  { <username> => { pw => $pw_obj,  groups => [...] } }
my %group_info;
# group_info: { <gid> => { gr => $gr_obj, users => [$users] }

# iterate over users and build a hash keyed by user *name* and one keyed by the
# users primary *gid*
foreach my $u (@user_list) {
    my $pw = getpwnam($u);
    unless (defined $pw) {
        warn "user $u does not exist";
        next;
    }
    # create per user user_info key
    $user_info{$u}{'pw'} = $pw;
    # create group_info entry for users primary gid
    push @{$group_info{$pw->gid}{'members'}}, $u;
}

if ($debug) {
    warn "# %user_info\n";
    warn Dumper(\%user_info);
}

# We are building both a gid list and add to the per user groups list.  This is
# needed because on Linux, puppet is unable to directly manage group membership
# via the Group resource type.  User membership in groups is managed via the
# User resource.
foreach my $g (@group_list) {
    my $gr = getgrnam($g);
    unless (defined $gr) {
        warn "group $g does not exist";
        next;
    }
    foreach my $u (@{$gr->members}) {
        unless (defined $user_info{$u}) {
           warn "user $u, member of group $g, is not being processed or does not exist" if $debug;
           next;
        }
            
        # populate the per user name group list
        push @{$user_info{$u}{'groups'}}, $g;
        # create a group_info entry for this group
        push @{$group_info{$gr->gid}{'members'}}, $u;
        #push @{$group_info{$gr->gid}}, $u;
    }
    $group_info{$gr->gid}->{'gr'} = $gr;
}

if ($debug) {
    warn "# %group_info\n";
    warn Dumper(\%group_info);
}

#
# generate output
#

$tt->process('puppet_class_header.tt', $vars) || die $tt->error(), "\n";

foreach my $u (keys %user_info) {
    process_realize_user($tt, $u);
}

print "\n";

# hash of lists.  hash key is gid, value is a list of usernames
foreach my $u (keys %user_info) {
    process_virtual_user($tt, $user_info{$u});
}

process_virtual_groups($tt, \%group_info);

$tt->process('puppet_class_footer.tt', $vars) || die $tt->error(), "\n";

### END SCRIPT
exit(0);

sub read_config_file {
    my $filename = shift;

    my @list;
    open(my $fh, $filename) or die "can't open file: $!";
    foreach my $line (<$fh>) {
        chomp $line;
        # ignore blank lines
        next unless $line =~ /\S/;
        # skip comments
        next if $line =~ /^\s+#/;

        push @list, $line,
    }
    close($fh) or die "can't close file: $!";

    return @list;
}

sub process_realize_user {
    my ($tt, $u) = @_;

    my $vars = {
        name => $u,
    };

    $tt->process('realize_user.tt', $vars) || die $tt->error(), "\n";
}

sub process_virtual_user {
    my ($tt, $u_hash) = @_;

    my $pw = $u_hash->{'pw'};

    use Data::Dumper;
    #print Dumper($pw);

    my $vars = {
        name    => $pw->name,
        passwd  => $pw->passwd,
        uid     => $pw->uid,
        gid     => $pw->gid,
        gecos   => $pw->gecos,
        dir     => $pw->dir,
        shell   => $pw->shell,
        groups  => $u_hash->{'groups'},
    };
    $tt->process('virtual_user.tt', $vars) || die $tt->error(), "\n";

    my $keys = $pw->dir . "/.ssh/authorized_keys";

    if (-e $keys) {
    open(my $fh, $pw->dir . "/.ssh/authorized_keys") or die "can't open file: $!";
    foreach my $line (<$fh>) {
	next unless $line =~ /\S/;
        my $key = Net::SSH::AuthorizedKey->parse( $line );
        use Data::Dumper;
#         print Dumper($key);
        my $comment = $key->comment;
        chomp $comment;

        # XXX ugly hack to work around puppets use of the ssh key 'name' a globally unique id
        my $key_digest = md5_base64($key->key);
        # use only the first 4 digits as the digest
	$key_digest = substr($key_digest, 0, 4);
        $comment = $comment . " # $pw->name $key_digest";
        $comment  =~ s/\s/_/g;
        
        my $vars = {
            key => $key->key,
            type => $key->encryption,
            username => $pw->name,
            comment => $comment,
        };
        $tt->process('ssh_authorized_key.tt', $vars) || die $tt->error(), "\n";
    }
    close ($fh);
    }
}

sub process_virtual_groups {
    my ($tt, $group_info) = @_;

    #print Dumper($group_info);

    foreach my $gid (keys %{$group_info}) {
        #my $gr = getgrgid($gid);
        my $gr = $group_info->{$gid}->{'gr'};
        #print Dumper($gr);
        next unless defined $gr;

        my $vars = {
            group_name => $gr->name,
            gid        => $gid,
            members    => $group_info->{$gid}{'members'},
        };

        $tt->process('virtual_group.tt', $vars) || die $tt->error(), "\n";
    }
}

=pod

=head1 NAME

ppwent - generate puppet user/group resources from pwent()/grent() 

=head1 SYNOPSIS

    ppwent --users <file name> --groups <file name> [--class_name my::users]

=head1 DESCRIPTION

=head1 CREDITS

Just me, myself, and I.

=head1 SUPPORT

Please contact the author directly via e-mail.

=head1 AUTHOR

Joshua Hoblitt <jhoblitt@cpan.org>

=head1 COPYRIGHT

Copyright (C) 2012  Joshua Hoblitt

This program is free software; you can redistribute it and/or
modify it under the terms of the GNU General Public License
as published by the Free Software Foundation; either version 2
of the License, or (at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.

The full text of the license can be found in the LICENSE file included with
this module or in the L<perlgpl> Pod included with Perl 5.8.1 or later.

=head1 SEE ALSO

L<File::Integrity>, L<File::Integrity::Stat>

=cut
